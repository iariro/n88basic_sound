1000 ' MML Player
1010 ' 2019/05/09 kumagai 実装開始
1020 ' 2019/07/05 kumagai 実機上でデバッグ
1030 ' -----------------------------------------------------------------------------
1040 ' 【概要】
1050 ' 指定のファイルの音楽データを再生する。
1060 '
1070 ' 【データ形式】
1080 ' ファイルの形式は以下の通り。
1090 '
1100 ' [コマンド]
1110 ' [データ]
1120 ' 　　：
1130 ' （繰り返し）
1140 ' 　　：
1150 '
1160 ' コマンド：'
1170 ' 意味：コメント
1180 ' データ：なし
1190 '
1200 ' コマンド：VOICE
1210 ' 意味：FM音源の音色変更
1220 ' データ：１行目に音色番号を指定。
1230 '         ２−６行目にVOICE命令用の整数の配列をカンマ区切りで指定。
1240 '
1250 ' コマンド：MML
1260 ' 意味：MMLの再生
1270 ' データ：再生するMML文字列。つねに６チャンネル分＝６行を指定すること。
1280 ' -----------------------------------------------------------------------------
1290 ' 初期化
1300 CLEAR,&H9000
1310 PLAY ALLOC 2550,2550,2550,2550,2550,2550
1320 DEFINT A-Z
1330 OPTION BASE 0
1340 DIM V(4, 9)
1350 DIM MML(6)
1360 ' ファイル読み込み
1370 INPUT "file name? ", FILE$
1380 OPEN FILE$ FOR INPUT AS #1
1390 LN = 0
1400 LOOP = 1
1410 WHILE LOOP=1 AND EOF(1)=0
1420 INPUT #1,COMD$
1430 LN = LN + 1
1440 IF MID$(COMD$,1,1) = "'" THEN PRINT MID$(COMD$,2) : GOTO *CONTINUE
1450 IF COMD$ = "VOICE" THEN GOSUB *VC : GOTO *CONTINUE
1460 IF COMD$ = "MML" THEN GOSUB *MML : GOTO *CONTINUE
1470 PRINT USING "error '@' in ###"; COMD$; LN
1480 LOOP=0
1490 *CONTINUE
1500 WEND
1510 CLOSE #1
1520 ' 終了
1530 END
1540 ' -----------------------------------------------------------------------------
1550 ' 音色変更
1560 *VC
1570 INPUT #1, VN
1580 FOR I=0 TO 4
1590 INPUT #1,V(I,0),V(I,1),V(I,2),V(I,3),V(I,4),V(I,5),V(I,6),V(I,7),V(I,8),V(I,9)
1600 NEXT
1610 VOICE VN, V
1620 RETURN
1630 '
1640 ' MML再生
1650 *MML
1660 FOR I=0 TO 5
1670 INPUT #1,MML$(I)
1680 NEXT I
1690 PLAY MML$(0),MML$(1),MML$(2),MML$(3),MML$(4),MML$(5)
1700 RETURN
